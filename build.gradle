apply plugin: 'groovy'
apply plugin: 'idea'
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
    compile 'javax:javaee-api:7.0'
    compile 'org.jenkins-ci.main:jenkins-core:2.46.1'
    compile 'org.jenkins-ci.plugins:ssh-slaves:1.17@jar'
    compile 'org.jenkins-ci.plugins:script-security:1.27@jar'
    compile 'org.apache.ant:ant:1.9.4'
    compile 'com.github.briandilley.jsonrpc4j:jsonrpc4j:1.1'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile "com.amazonaws:aws-java-sdk:${sdkAwsVersion}"
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
        exclude(module: 'groovy')
    }

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2'
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.2@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.0@jar'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

def sysProperties = System.getProperties() as Map<String, ?>

task generatorView(dependsOn: 'classes', type: JavaExec) {
    main = 'com.anselmopfeifer.view.generatorView'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties sysProperties
}